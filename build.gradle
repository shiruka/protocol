plugins {
  id "java"
  id "java-library"
  id "checkstyle"
  id "maven-publish"
  id "signing"
  id "io.github.gradle-nexus.publish-plugin" version "1.1.0"
}

group "io.github.shiruka"

java {
  sourceCompatibility = JavaVersion.VERSION_17
  targetCompatibility = JavaVersion.VERSION_17
}

compileJava {
  options.deprecation true
  options.encoding "UTF-8"
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier "sources"
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  javadoc.options.encoding = "UTF-8"
  javadoc.options.tags = ["todo"]
  javadoc.options.addBooleanOption("html5", true)
  classifier "javadoc"
  from javadoc.destinationDir
}

checkstyle {
  configFile = rootProject.file("checkstyle.xml")
  checkstyleTest.enabled = false
}

test {
  useJUnitPlatform()
}

artifacts {
  archives sourcesJar
  archives javadocJar
}

configurations {
  testImplementation.extendsFrom compileOnlyApi
}

repositories {
  mavenCentral()
  maven { url "https://jitpack.io" }
  maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
  mavenLocal()
}

dependencies {
  compileOnlyApi "org.jetbrains:annotations:23.0.0"
  annotationProcessor "org.jetbrains:annotations:23.0.0"

  compileOnlyApi "org.projectlombok:lombok:1.18.22"
  annotationProcessor "org.projectlombok:lombok:1.18.22"

  compileOnlyApi "io.github.shiruka:api:3.0.14-SNAPSHOT"
  compileOnlyApi "io.github.shiruka:network:3.0.5-SNAPSHOT"

  compileOnlyApi "it.unimi.dsi:fastutil:8.5.8"
  compileOnlyApi "io.netty:netty-all:4.1.74.Final"
  compileOnlyApi "it.unimi.dsi:fastutil-core:8.5.8"
  compileOnlyApi "com.nimbusds:nimbus-jose-jwt:9.20"
  compileOnlyApi "com.google.guava:guava:31.1-jre"
  compileOnlyApi "org.apache.commons:commons-math3:3.6.1"
  compileOnlyApi "org.apache.logging.log4j:log4j-api:2.17.2"
  compileOnlyApi "org.apache.logging.log4j:log4j-iostreams:2.17.2"
  compileOnlyApi "org.apache.logging.log4j:log4j-slf4j-impl:2.17.2"
  compileOnlyApi "com.fasterxml.jackson.core:jackson-databind:2.13.1"

  testAnnotationProcessor "org.jetbrains:annotations:23.0.0"
  testAnnotationProcessor "org.projectlombok:lombok:1.18.22"
  testImplementation "org.junit.jupiter:junit-jupiter:5.8.2"
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      groupId project.group
      artifactId project.name
      version project.version

      from components.java
      artifact tasks.sourcesJar
      artifact tasks.javadocJar
      pom {
        name = "Shiru ka Protocol"
        description = "A protocol library to handle Minecraft: Bedrock Edition packets."
        url = "https://shiruka.github.io/"
        licenses {
          license {
            name = "MIT License"
            url = "https://mit-license.org/license.txt"
          }
        }
        developers {
          developer {
            id = "portlek"
            name = "Hasan Demirta≈ü"
            email = "utsukushihito@outlook.com"
          }
        }
        scm {
          connection = "scm:git:git://github.com/shiruka/protocol.git"
          developerConnection = "scm:git:ssh://github.com/shiruka/protocol.git"
          url = "https://github.com/shiruka/protocol"
        }
      }
    }
  }
}

signing {
  useGpgCmd()
  sign publishing.publications.mavenJava
}

nexusPublishing {
  repositories {
    sonatype()
  }
}
